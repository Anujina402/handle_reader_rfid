plugins {
    id 'com.android.application'
}

android {
    namespace 'com.pda.uhf_g'
    compileSdk 33
    defaultConfig {
        applicationId "com.pda.uhf_g"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 36
        versionName "3.6"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "uhfg_v${variant.versionName}.apk"
        }
    }
    buildTypes {
        debug {
            buildConfigField("String", "BUILD_TIME", getDate())
        }
        release {
            buildConfigField("String", "BUILD_TIME", getDate())
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.jetbrains.kotlin' &&
                details.requested.name.startsWith('kotlin-stdlib')) {
            details.useVersion('1.8.10')
            details.because('Align Kotlin stdlib artifacts to a single version to avoid duplicate classes')
        }
    }
    resolutionStrategy.dependencySubstitution {
        substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk7') using module('org.jetbrains.kotlin:kotlin-stdlib:1.8.10')
        substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk8') using module('org.jetbrains.kotlin:kotlin-stdlib:1.8.10')
    }
}


static String getDate() {
    Date date = new Date()
    return "\"" + date.format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("GMT+08:00")) + "\""
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.10')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation files('libs/reader(1).jar')
    implementation files('libs/ModuleAPI_J.jar')
    implementation files('libs/App_Demo_API.jar')
    implementation files('libs/DeviceAPIver20150204.jar')
    implementation files('libs/logutil-1.5.1.1.jar')
    implementation files('libs/UHF67_v3.6.jar')
    //implementation files('libs/UHF67_v1.0.15.jar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'net.sourceforge.jexcelapi:jxl:2.6.12'
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

}
